plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "application"
}

group 'com.phoenix.authservice'
version '0.0.1'
mainClassName = "io.ktor.server.netty.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-jackson:$ktor_version"

    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-dao:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jodatime:$exposed_version"

    implementation "com.zaxxer:HikariCP:4.0.1"
    implementation "org.postgresql:postgresql:42.2.18"
    implementation "org.flywaydb:flyway-core:7.5.2"

    implementation "io.github.cdimascio:java-dotenv:5.2.2"

    implementation "org.koin:koin-ktor:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"

    testImplementation "io.mockk:mockk:1.10.5"

    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-auth-jwt:$ktor_version"
    implementation "io.ktor:ktor-network-tls-certificates:$ktor_version"

    implementation "org.mindrot:jbcrypt:0.4"
}

shadowJar {
    archivesBaseName = "authservice"
    classifier("")
    version("")
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = "full"
        events = ["skipped", "passed", "failed"]
    }
}

task generateJks(type: JavaExec, dependsOn: 'classes') {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.phoenix.authservice.config.CertificateGenerator'
}

getTasksByName("run", false).first().dependsOn('generateJks')
